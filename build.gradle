plugins {
    id 'application'
    id 'io.freefair.lombok' version '5.3.0'
    id 'jacoco'
}
apply plugin : 'java' 

group = 'com.jobsity.bowlingscoreboard'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '8'

repositories {
    mavenCentral()
}
	
dependencies {
    testImplementation 'org.mockito:mockito-core:3.8.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:3.8.0'
    testImplementation 'org.assertj:assertj-core:3.19.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.1'
    testImplementation 'io.cucumber:cucumber-java:6.8.1'
    
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.1'
}

configurations {
    acceptanceTestRuntime {
        extendsFrom testImplementation
    }
}

test {
	useJUnitPlatform()
	excludes = ['**/*Application.java']
}

jar {
    manifest {
        attributes 'Main-Class': 'com.jobsity.bowlingscoreboard.Application'
    }
}

ext {
   javaMainClass = 'com.jobsity.bowlingscoreboard.Application'
}

application {
    mainClassName = javaMainClass
}

task acceptanceTest() {
    dependsOn assemble, testClasses
    doLast {
        javaexec {
            main = "io.cucumber.core.cli.Main"
            classpath = configurations.acceptanceTestRuntime + sourceSets.main.output + sourceSets.test.output
            args = ['--plugin', 'pretty', '--glue', 'com.jobsity.bowlingscoreboard.features', 'src/test/resources/features']
        }
    }
}

def jacocoExclude = [
	'**/*Application.class',
]

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
    }
    
    afterEvaluate {
        afterEvaluate {
            getClassDirectories().setFrom(classDirectories.files.collect {
                fileTree(dir: it, exclude: jacocoExclude)
            })
        }
    }
}

check.dependsOn jacocoTestReport

tasks.test {
    finalizedBy(tasks.jacocoTestReport)
}
